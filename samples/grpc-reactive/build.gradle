plugins {
	id 'java'
	id 'org.springframework.boot' version '4.0.0'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'com.google.protobuf' version '0.9.4'
}

group = 'com.example'
version = '1.0.0-SNAPSHOT'

def toolchainVersion() {
	if (project.hasProperty('testToolchain')) {
		return project.property('testToolchain').toString().toInteger()
	}
	return 17
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(toolchainVersion())
	}
}

repositories {
    mavenCentral()
	mavenLocal()
	maven { url 'https://repo.spring.io/milestone' }
    maven { url 'https://repo.spring.io/snapshot' }
}

def springGrpcVersion = project.properties['springGrpcVersion'] ?: '1.0.0-SNAPSHOT'

dependencyManagement {
    imports {
        mavenBom "org.springframework.grpc:spring-grpc-dependencies:${springGrpcVersion}"
    }
}

dependencies {
    implementation 'org.springframework.grpc:spring-grpc-spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'io.projectreactor:reactor-core'
	implementation 'com.salesforce.servicelibs:reactor-grpc-stub:1.2.4'

    compileOnly 'javax.annotation:javax.annotation-api:1.3.2'

	testImplementation 'org.springframework.grpc:spring-grpc-test'
    testImplementation 'io.projectreactor:reactor-test'

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testRuntimeOnly "io.netty:netty-transport-native-epoll::linux-x86_64"
}

test {
    useJUnitPlatform()
    testLogging.showStandardStreams = true
    outputs.upToDateWhen { false }
}

protobuf {
	protoc {
		artifact = "com.google.protobuf:protoc:${dependencyManagement.importedProperties['protobuf-java.version']}"
	}
	plugins {
		grpc {
			artifact = "io.grpc:protoc-gen-grpc-java:${dependencyManagement.importedProperties['grpc.version']}"
		}
		reactor {
			artifact = "com.salesforce.servicelibs:reactor-grpc:1.2.4"
		}
	}
	generateProtoTasks {
		all()*.plugins {
			grpc {
				option '@generated=omit'
			}
			reactor {}
		}
	}
}
